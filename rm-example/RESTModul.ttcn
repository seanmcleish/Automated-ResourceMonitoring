module RESTModul{

	import from HTTPmsg_PortType all;
	import from HTTPmsg_Types all;
	import from JSONModul all;
	type record of SensorInstance SensorInstances;
	
	type record ServiceInstance
    {
      charstring hostname,
      integer portnumber
    }
    
    type record SensorInstance
    {
      charstring hostname optional,
      integer portnumber optional,
      JSONObject.Measurement measurement optional,
      JSONObject.SystemObject measurementSystem optional,
      JSONObject.Processes measurementProcesses optional
    }
    
    
    
    function AddSensor(in charstring p_hostname, in integer p_portnumber, inout SensorInstances p_sensorInstances)
    {
      if(not(ispresent(p_sensorInstances)))
      {
        p_sensorInstances := {};
      }
      p_sensorInstances[lengthof(p_sensorInstances)] := {hostname := p_hostname, portnumber := p_portnumber, measurement := omit, measurementSystem := omit, measurementProcesses := {}};
    }
    function AddProcessToSensor(inout SensorInstance p_sensor, in charstring p_processname )
    {
	  if(not(ispresent(p_sensor.measurementProcesses)))
      {
        p_sensor.measurementProcesses := {};
      }
      p_sensor.measurementProcesses[lengthof(p_sensor.measurementProcesses)] := 
      {
      processId := lengthof(p_sensor.measurementProcesses)+1, 
      processName := p_processname, 
      processPid := omit
      };
    }
	
	type component TestComponent 
	{
		port HTTPmsg_PT TestPort;
	}
	
	type union RESTRequest
	{
	  HTTPMessage t_GET_Request,
	  HTTPMessage t_PUT_Request,
	  HTTPMessage t_POST_Request,
	  HTTPMessage t_DELETE_Request
	}

	template HTTPMessage t_GET_Request(in charstring p_path, in charstring p_body := "") := 
		{
		  request := 
			{ 
			client_id := omit, 
			method := "GET", 
			uri := p_path, 
			version_major := 1, 
			version_minor := 1,
			header := {}, 
			body := p_body
			}
		}
	template HTTPMessage t_PUT_Request(in charstring p_path, in charstring p_body := "") := 
		{
		  request := 
			{ 
			client_id := omit, 
			method := "PUT", 
			uri := p_path, 
			version_major := 1, 
			version_minor := 1, 
			header := {}, 
			body := p_body
			}
		}
	template HTTPMessage t_POST_Request(in charstring p_path, in charstring p_body := "") := 
		{
		  request := 
			{ 
			client_id := omit, 
			method := "POST", 
			uri := p_path, 
			version_major := 1, 
			version_minor := 1, 
			header := 	{}, 
			body := p_body
			}
		}
	template HTTPMessage t_DELETE_Request(in charstring p_path, in charstring p_body := "") := 
		{
		  request := 
			{ 
			client_id := omit, 
			method := "DELETE", 
			uri := p_path, 
			version_major := 1, 
			version_minor := 1, 
			header := {}, 
			body := p_body
			}
		}
	function SendRESTRequest(in ServiceInstance p_serviceInstance, in template HTTPMessage t_request) runs on TestComponent return JSONObject
    {
      map(self:TestPort, system:TestPort);
      var JSONObject v_JSON_PDU;
      
      // ==== CONNECTION-PART (ESTABLISH SOCKETCONNECTION) ====
      var Connect_result v_Connect_result;
	  template Connect t_connect := { hostname := p_serviceInstance.hostname, portnumber := p_serviceInstance.portnumber, use_ssl := false };
	  template Close t_close := { client_id := omit};
	  timer t0:=20.0;    
      TestPort.send(t_connect) ;
      t0.start;
      alt 
      {
        []TestPort.receive(Connect_result:?) -> value v_Connect_result 
        { 
        }
        []any port.receive
        {
          log("Timeout: A connection to the service could not established. Aborting...");
          return v_JSON_PDU;
        }
      }
      
      // ==== MESSAGE-PART (SEND HTTP_MESSAGE) ====
      var HTTPMessage  v_HTTPMessage_response;
      t_request.request.header[lengthof(t_request.request.header)] :=
      { 
  		header_name := "Host", 
  		header_value := p_serviceInstance.hostname & ":" & int2str(p_serviceInstance.portnumber)
	  }
      timer t1 := 30.0;
      t1.start;
      TestPort.send(t_request)
      alt 
      {
        []TestPort.receive(HTTPMessage:?)  -> value v_HTTPMessage_response
        {
          if (ischosen(v_HTTPMessage_response.response_binary))
          {
            log(oct2unichar(v_HTTPMessage_response.response_binary.body, "UTF-8"));
            v_JSON_PDU := dec_JSON(v_HTTPMessage_response.response_binary.body);
          }
          if (ischosen(v_HTTPMessage_response.response))
          {
            v_JSON_PDU := dec_JSON(char2oct(v_HTTPMessage_response.response.body));
          }
        }
        []t1.timeout
        {
          log("Timeout: Sending a message to the service failed. Aborting...");
          log(v_JSON_PDU);
          return v_JSON_PDU;
        }
      }

      // ==== CLOSING-PART ====
      TestPort.send( t_close );
      
      return v_JSON_PDU;
  }

}
