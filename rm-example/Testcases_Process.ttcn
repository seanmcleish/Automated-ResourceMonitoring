module Testcases_Process {

import from HTTPmsg_Types all;
import from RESTModul all;
import from JSONModul all;
import from Helper all;

testcase tc_createProcessesWithMeasurementId(in ServiceInstance p_serviceInstance, in integer p_measurementId, in JSONObject.Process p_process, out JSONObject.Processes p_result)
runs on TestComponent
{
  map(self:TestPort, system:TestPort);
  var template HTTPMessage post_request := t_POST_Request( "/process?measurementId="&int2str(p_measurementId)&"&processName="&p_process.processName);
  
  var JSONObject response := SendRESTRequest(p_serviceInstance, post_request);

  if(ischosen(response.Processes))
  {
    if(not(ispresent(response.Processes[0].processPid)))
    {
      setverdict(fail, "Process with name " & response.Processes[0].processName & " is not running");
    }
    else
    {
      setverdict(pass, "Added processes with name " & p_process.processName &" succesfully for measurement with id " & int2str(p_measurementId));
      p_result:=response.Processes;
    }
    
  }
  else
  {
    setverdict(none, "No processes added for name " & p_process.processName);
  }

  
}

testcase tc_processMemoryUsageGreaterThan(in JSONObject.Process p_process, in JSONObject p_values, in integer p_memoryTotalSpace, in float p_relativeUpperBound)
runs on TestComponent 
{
  if(not(ischosen(p_values.processValues)))
  {
    setverdict(none, "No results given for checking process memory usage for process with name "&p_process.processName & " with PID " & int2str(p_process.processPid));
    stop;
  }
  setverdict(pass, "Relative process memory usage was not overstepped for process " & p_process.processName &" - UpperBound: "& float2str(p_relativeUpperBound));
  
  for(var integer i := 0; i<lengthof(p_values.processValues); i := i+1)
  {

    if((int2float(p_values.processValues[i].processValueResidentSetSize)/int2float(p_memoryTotalSpace)) > p_relativeUpperBound)
    {
      setverdict(fail, "Relative process memory usage was overstepped for process " & p_process.processName &" - UpperBound: "& float2str(p_relativeUpperBound) & " Calculated: " & float2str(int2float(p_values.processValues[i].processValueResidentSetSize)/int2float(p_memoryTotalSpace)));
      stop;
    }
  }
}

testcase tc_processMemoryUsageSlopeGreaterThan(in JSONObject.Process p_process, in JSONObject p_values, in float p_relativeSlopeUpperBound, in integer p_expected_mttf)
runs on TestComponent 
{
  if(not(ischosen(p_values.processValues)))
  {
    setverdict(none, "No results given for checking process memory usage for process with name "&p_process.processName & " with PID " & int2str(p_process.processPid));
    stop;
  }
  if(lengthof(p_values.processValues) < 5)
  {
    setverdict(none, "Not enough results given for checking process memory usage for process with name "&p_process.processName & " with PID " & int2str(p_process.processPid));
    stop;
  }
  var IntegerDictionary v_values := {};
  for(var integer i := 0; i<lengthof(p_values.processValues); i := i+1)
  {
    var KeyValueInteger v_value := 
    { 
    	Key := DatestringToRoughlySeconds(p_values.processValues[i].processValueId.processValueTimestamp)-DatestringToRoughlySeconds(p_values.processValues[0].processValueId.processValueTimestamp),
    	Value := p_values.processValues[i].processValueResidentSetSize
    }
    v_values[i] := v_value;
  }
  var RegressionParameter v_regressionResult := CalculateRegressionInteger(v_values);
  var integer v_current_mttf :=  CalculateMttf(v_regressionResult, v_regressionResult.interceptParameter+p_relativeSlopeUpperBound);
  log_debug("CALCULATED MTTF IN SECONDS = " & int2str(v_current_mttf));
  if(v_current_mttf == -1 or v_current_mttf == -2)
  {
    setverdict(none, "No results or no trend detected for process memory usage for process " & p_process.processName & " with PID "& int2str(p_process.processPid) &" - Allowed MTTF: " & int2str(p_expected_mttf) & "s Caulculated: " & int2str(v_current_mttf) & "s" );
  }
  else if(v_current_mttf == -3)
  {
    setverdict(inconc, "Negative MTTF calculated for process memory usage for process " & p_process.processName & " with PID "& int2str(p_process.processPid) &" - Bound could be already overstepped(fail) or negative trend detected(pass) " );
  }
  else if(v_current_mttf < p_expected_mttf)				// IF CURRENT_MTTF < MIN_EXPECTED_MTTF => FAIL
  {
    setverdict(fail, "MTTF not hight enough for process memory usage for process " & p_process.processName & " with PID "& int2str(p_process.processPid) &" - Allowed MTTF: " & int2str(p_expected_mttf) & "s Caulculated: " & int2str(v_current_mttf) & "s" );
  }
  else{
    setverdict(pass, "MTTF hight enough for process memory usage for process " & p_process.processName & " with PID "& int2str(p_process.processPid) &" - Allowed MTTF: " & int2str(p_expected_mttf) & "s Caulculated: " & int2str(v_current_mttf) & "s" );
  }
}
testcase tc_processHandlesSlopeGreaterThan(in JSONObject.Process p_process, in JSONObject p_values, in float p_relativeSlopeUpperBound, in integer p_expected_mttf)
runs on TestComponent 
{
  if(not(ischosen(p_values.processValues)))
  {
    setverdict(none, "No results given for checking process handles count for process with name "&p_process.processName & " with PID " & int2str(p_process.processPid));
    stop;
  }
  if(lengthof(p_values.processValues) < 5)
  {
    setverdict(none, "Not enough results given for checking process handles count for process with name "&p_process.processName & " with PID " & int2str(p_process.processPid));
    stop;
  }
  var IntegerDictionary v_values := {};
  for(var integer i := 0; i<lengthof(p_values.processValues); i := i+1)
  {
    var KeyValueInteger v_value := 
    { 
    	Key := DatestringToRoughlySeconds(p_values.processValues[i].processValueId.processValueTimestamp)-DatestringToRoughlySeconds(p_values.processValues[0].processValueId.processValueTimestamp),
    	Value := p_values.processValues[i].processValueHandles
    }
    v_values[i] := v_value;
  }
  var RegressionParameter v_regressionResult := CalculateRegressionInteger(v_values);
  var integer v_current_mttf :=  CalculateMttf(v_regressionResult, v_regressionResult.interceptParameter+p_relativeSlopeUpperBound);
  log_debug("CALCULATED MTTF IN SECONDS = " & int2str(v_current_mttf));
    if(v_current_mttf == -1 or v_current_mttf == -2)
  {
    setverdict(none, "No results or no trend detected for process handle count for process " & p_process.processName & " with PID "& int2str(p_process.processPid) &" - Allowed MTTF: " & int2str(p_expected_mttf) & "s Caulculated: " & int2str(v_current_mttf) & "s" );
  }
  else if(v_current_mttf == -3)
  {
    setverdict(inconc, "Negative MTTF calculated for process handle count for process " & p_process.processName & " with PID "& int2str(p_process.processPid) &" - Bound could be already overstepped(fail) or negative trend detected(pass) " );
  }
  else if(v_current_mttf < p_expected_mttf)				// IF CURRENT_MTTF < MIN_EXPECTED_MTTF => FAIL
  {
    setverdict(fail, "MTTF not hight enough for process handle count for process " & p_process.processName & " with PID "& int2str(p_process.processPid) &" - Allowed MTTF: " & int2str(p_expected_mttf) & "s Caulculated: " & int2str(v_current_mttf) & "s" );
  }
  else{
    setverdict(pass, "MTTF hight enough for process handle countfor process " & p_process.processName & " with PID "& int2str(p_process.processPid) &" - Allowed MTTF: " & int2str(p_expected_mttf) & "s Caulculated: " & int2str(v_current_mttf) & "s" );
  }
}

testcase tc_getProcessDataByPid(in ServiceInstance p_serviceInstance, in integer p_measurementId, in integer p_processId, out JSONObject p_result) runs on TestComponent
{
  var template HTTPMessage get_request := t_GET_Request( "/process/" &int2str(p_processId)&"/processValues/" &int2str(p_measurementId));
  p_result := SendRESTRequest(p_serviceInstance,get_request);
  if(not(ischosen(p_result.processValues)))
  {
    setverdict(none, "No results received process with id "& int2str(p_processId));
    stop;
  }
  setverdict(none, "Results received process with id "& int2str(p_processId));

}

}
